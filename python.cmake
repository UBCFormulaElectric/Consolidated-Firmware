# Checks for
# - Python
# - Pip
# - Pipenv if using virtual environment (option below)
# OUTPUTS
# PYTHON_COMMAND: The command to run python scripts
# PIP_COMMAND: The command to run pip

message("")
message("üêç Python Configuration")

# ====== Generate PYTHON_COMMAND ======
find_package(Python3 3.10.0...3.13.7 COMPONENTS Interpreter REQUIRED)
message("  ‚ÑπÔ∏è Found Python ${Python3_VERSION}")
set(PYTHON_COMMAND ${Python3_EXECUTABLE})

find_program(PIPENV_COMMAND pipenv)
IF (PIPENV_COMMAND)
    message("  ‚ÑπÔ∏è Found pipenv: ${PIPENV_COMMAND}")
    message("  üìü Using pipenv command: ${PIPENV_COMMAND}")

    # Determine the python command to use within pipenv
    IF (${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Windows")
        set(PYTHON_BASE_COMMAND python)
    ELSEIF (${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Darwin")
        set(PYTHON_BASE_COMMAND python3)
    ELSEIF (${CMAKE_HOST_SYSTEM_NAME} STREQUAL "Linux")
        set(PYTHON_BASE_COMMAND python3)
    ELSE ()
        message(FATAL_ERROR "‚ùå Unsupported host system: ${CMAKE_HOST_SYSTEM_NAME}")
    ENDIF ()
    set(PYTHON_COMMAND ${PIPENV_COMMAND} run ${PYTHON_BASE_COMMAND})

    # check that pipenv exists
    execute_process(COMMAND ${PIPENV_COMMAND} --venv
            OUTPUT_VARIABLE FIND_PIPENV_OUTPUT
            RESULT_VARIABLE FIND_PIPENV_RESULT
            ERROR_VARIABLE FIND_PIPENV_ERROR
            OUTPUT_STRIP_TRAILING_WHITESPACE
            ERROR_STRIP_TRAILING_WHITESPACE)
    IF (${FIND_PIPENV_RESULT})
        message(FATAL_ERROR "Pipenv path report error: ${FIND_PIPENV_RESULT} ${FIND_PIPENV_ERROR}
            Make sure that you have ran \"pipenv install\" in the root directory")
    ENDIF ()
    message("  üõ£ Pipenv path: ${FIND_PIPENV_OUTPUT}")

    # check that dependencies are installed with pipenv
    message("  üì¶ Updating Pipenv dependencies...")
    execute_process(COMMAND ${PIPENV_COMMAND} sync RESULT_VARIABLE PIPENV_STATUS OUTPUT_QUIET)
    IF (NOT ${PIPENV_STATUS} EQUAL 0)
        message(FATAL_ERROR "‚ùå Error setting up pipenv. Please install/fix pipenv and try again.")
    ENDIF ()
    message("  üì¶ Pipenv Dependencies Updated")
ELSE ()
    message("   ‚ùå pipenv not found. If you would like to use a virtual environment, install it with \"pip install pipenv\"")
    message("  üîç Finding pip...")
    set(PIP_COMMAND ${PYTHON_COMMAND} -m pip)
    execute_process(COMMAND ${PIP_COMMAND} --version RESULT_VARIABLE PIP_CHECK_STATUS)
    IF (NOT ${PIP_CHECK_STATUS} EQUAL 0)
        message(FATAL_ERROR "‚ùå Could not find pip. Please install pip and try again.")
    ENDIF ()
    message("  üìü Using pip with \"${PIP_COMMAND}\"")

    execute_process(COMMAND ${PIP_COMMAND} install -r ${CMAKE_SOURCE_DIR}/environment/requirements.txt
            RESULT_VARIABLE PIP_INSTALL_STATUS
            COMMAND_ERROR_IS_FATAL ANY
    )
    IF (NOT ${PIP_INSTALL_STATUS} EQUAL 0)
        message(FATAL_ERROR "‚ùå Could not successfully install Python dependencies")
    ENDIF ()
    message("  üì¶ Pip packages collected")
ENDIF ()
message("  üìü Using Python Command: \"${PYTHON_COMMAND}\"")
message("  ‚úÖ Python dependencies installed")
